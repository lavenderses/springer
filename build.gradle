import com.github.spotbugs.snom.SpotBugsTask

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.3.0"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.6"
    }
}

group 'org.nakanoi'
version '0.1.0-SNAPSHOT'

subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "checkstyle"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.github.spotbugs"

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
    }

    spotless {
        format 'misc', {
            target '*.gradle'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
        java {
            importOrder()
            removeUnusedImports()

            String formatVersion = '1.13.0'
            googleJavaFormat formatVersion
        }
    }

    checkstyle {
        toolVersion = '8.45'
    }

    tasks.withType(SpotBugsTask) {
        reports {
            xml.required.set(true)
            html.required.set(true)
        }
    }
}
